import org.apache.tools.ant.taskdefs.condition.Os

apply plugin: 'com.android.library'
apply plugin: 'com.novoda.bintray-release'

publish {
    userOrg = 'urqa'
    groupId = 'urqa.io'
    artifactId = 'urqa-client'
    publishVersion = '0.9.9'
    desc = 'This is the client sdk for android application to report the crash to the UrQA server when the runtime exceptions or errors are occurred'
    website = 'https://github.com/UrQA/UrQA-Client-Android-Gradle'
    issueTracker = "${website}/issues"
    repository = "${website}.git"
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {

    }
}

repositories {
    jcenter()
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:23.0.1'
    //compile files('libs/classes.jar')
}

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.1'

    sourceSets {
        main {
            java {
                srcDir 'src/main/java'
            }

            jni.srcDirs = []
            jniLibs.srcDir 'src/main/libs'
        }
    }

    defaultConfig {
        minSdkVersion 8
        targetSdkVersion 23
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }
}



task deleteOldJar(type: Delete) {
    delete 'release/urqa-client_0.9.9.jar'
}

task exportJar(type: Copy) {
    from('build/intermediates/bundles/release/')
    into('release/')
    include('classes.jar')
    rename('classes.jar', 'urqa-client_0.9.9.jar')
}

exportJar.dependsOn(deleteOldJar, build)


task ndkBuild(type: Exec, description:'Compile JNI source via NDK') {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())

    def command = properties.getProperty('ndk.dir')
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        command += "\\ndk-build.cmd"
    } else {
        command += "/ndk-build"
    }

    def isDebug = true
    if (isDebug) {
        commandLine command, 'NDK_DEBUG=1', '-C', file('src/main').absolutePath
    } else {
        commandLine command, '-C', file('src/main').absolutePath
    }
}




